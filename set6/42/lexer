%class-name Scanner

singleCharOperator  [\+\-\*\/\%\|\&\~\^\=<>!]
doubleCharOperator  \<\<|\>\>|\&\&|\|\||\+\+|\-\-
intSuffix           [uU]?([lLzZ]|ll|LL)

simpleEscapeSeq     \\[\'\"?\\abfnrtv]
numericEscapeSeq    \\(x[[:xdigit:]]{1,2}|[0-7]{1,3})
universalCharNam    \\(u[[:digit:]]{4}|U[[:digit:]]{8})
escapeSeq           {simpleEscapeSeq}|{numericEscapeSeq}|{universalCharNam}

digitSequence       [[:digit:]]*
hexDigitSeq         [[:xdigit:]]*
exponent            [eEpP][+-]?{digitSequence}
floatSuffix         [fFlL]

encodingPrefix      ([uUL]|u8)
sChar               [^"\\\n]|{escapeSeq}
cChar               [^'\\\n]|{escapeSeq}
cCharSequence       {cChar}{2,}

%%
[[:blank:]]                     /* Ignore blanks */
[[:alpha:]_][[:alnum:]_]*       return IDENTIFIER;

    /* Floating point literals, according to
    https://en.cppreference.com/w/cpp/language/floating_literal */
{digitSequence}{exponent}{floatSuffix}?                      | /* (1) */
{digitSequence}\.{exponent}?{floatSuffix}?                   | /* (2) */
{digitSequence}?\.{digitSequence}{exponent}?{floatSuffix}?   | /* (3) */
(0x|0X){hexDigitSeq}\.?{exponent}{floatSuffix}?              | /* (4, 5) */
(0x|0X){hexDigitSeq}?\.{hexDigitSeq}{exponent}{floatSuffix}? return DOUBLE; /* (6) */

    /* Integer literals, according to
    https://en.cppreference.com/w/cpp/language/integer_literal */
[1-9][[:digit:]]*{intSuffix}?               | /* pos/negative allowed */
0[0-7]*{intSuffix}?                         | /* octal, allowing 000... */
(0x|0X)[[:xdigit:]]+{intSuffix}?            | /* hexadecimal */
(0b|0B)[01]+{intSuffix}?                    return INTEGRAL; /* binary */

    /* Operators from the following columns:
        - Assignment
        - Increment/decrement
        - Arithmetic
        - Logical
        - Comparison
    from https://en.cppreference.com/w/cpp/language/expressions#Operators */
{singleCharOperator}                        |
{doubleCharOperator}                        |
{singleCharOperator}=                       |
(\>\>|\<\<)=                                |
\<=\>                                       return OPERATOR;

    /* String literals, according to
    https://en.cppreference.com/w/cpp/language/string_literal */
{encodingPrefix}?\"({sChar}|\"\")*\"        return STRING;

    /* Character literals, according to
    https://en.cppreference.com/w/cpp/language/character_literal */
{encodingPrefix}?\'{cChar}\'                | /* (1, 2, 3, 4, 5) */
L?\'{cCharSequence}\'                       return CHAR; /* (6, 7) */

.                               return matched()[0];
